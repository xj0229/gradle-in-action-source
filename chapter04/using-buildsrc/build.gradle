import com.manning.gia.ProjectVersion
import com.manning.gia.ReleaseVersionTask

//gradle在buildSrc目录下使源文件结构标准化
//这些目录下的代码都会被自动编译，并且被加入到gradle构建脚本的classpath中
//运行gradle makeReleaseVersion

//有一个要点是，注意task动作和配置代码在构建生命周期的不同阶段执行。

ext.versionFile = file('version.properties')
project.version = readVersion()

ProjectVersion readVersion() {
    logger.quiet 'Reading the version file.'

    if (!versionFile.exists()) {
        throw new GradleException("Required version file does not exit: $versionFile.canonicalPath")
    }

    Properties versionProps = new Properties()

    versionFile.withInputStream { stream ->
        versionProps.load(stream)
    }

    new ProjectVersion(versionProps.major.toInteger(), versionProps.minor.toInteger(), versionProps.release.toBoolean())
}

task makeReleaseVersion(type: ReleaseVersionTask) {
    release = version.release
    destFile = versionFile
}
